// Now the actual enemies...
actor T1FlemoidAlt1 : PB_Monster replaces FlemoidusCommonusV3
{
    +DONTGIB
    +NOTDMATCH
    +NOICEDEATH
	+FLOORCLIP
	+SLIDESONWALLS
	+DOHARMSPECIES
	+HARMFRIENDS
	Painchance Freeze, 255
	painchance ice, 255
	painchance frost, 255
	Monster
	Radius 16
	Height 56
	speed 8
	fastspeed 8
	MaxStepHeight 24
	MaxDropOffHeight 32
	Mass 100
	Health 80
	BloodType "Green_Blood", "GreenSawBlood", "GreenSawBlood"
	bloodcolor green
	PainChance 200
	PainChance "Avoid", 64
	PainChance "Head", 255
	PainChance "Kick", 255
	PainChance "ExtremePunches", 255
	PainChance "Melee", 255
	PainChance "Taunt", 255
	PainChance "MonsterKnocked", 255
	PainChance "ExplosiveImpact", 255
	PainChance "Trample", 255
	PainChance "Stun", 255
	PainChance "Siphon", 255
	damagefactor "Crush", 5.0
	damagefactor "Avoid", 1.0
	damagefactor "GibRemoving", 0.0
	damagefactor "DontCallTheBaron", 0.0
	damagefactor "Blood", 0.0 
	damagefactor "BlueBlood", 0.0
	damagefactor "GreenBlood", 0.0
	damagefactor "Kick", 0.4
	damagefactor "Shrapnel", 0.4
	damagefactor "ExplosiveImpact", 0.6
	damagefactor "killme", 0.0
	damagefactor "Taunt", 1.0
	DamageFactor "CauseObjectsToSplash", 0.0
	damagefactor "SSG", 50.0
	damagefactor "Shotgun", 1.2
	damagefactor "Saw", 0.5
	damagefactor "Cut", 0.75
	damagefactor "Tear", 0.25
	SeeSound "monsters/Flemoid/see"
	PainSound "monsters/Flemoid/pain"
	DeathSound "monsters/Flemoid/death"
	ActiveSound "monsters/Flemoid/searching"
    States
    {
    Pain:
        POSS A 3 A_Pain
        Goto See
	Pain.Freeze:
	pain.ice:
	pain.frost:
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 0 A_ChangeFlag("SOLID", 0)
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItem("FrozenFlemoidAlt1")
		stop
	death.Freeze:
	death.ice:
	death.frost:
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 0 A_ChangeFlag("SOLID", 0)
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItem("FrozenFlemoidAlt1")
		stop
    XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_NoBlocking
		POSS PQRST 5
		POSS U 5
		Stop
    Death:
        POSS H 5
		POSS I 5 A_Scream
		POSS J 5 A_NoBlocking
		POSS K 5
		POSS L 5
		Stop
	Spawn:
		POSS AB 10 A_Look
		Loop
	See:
		POSS AABBCCDD 2 A_Chase
		Loop
	Melee:
		POSS E 5 A_FaceTarget
		POSS F 5 A_CustomMeleeAttack(random[posattack](1,5)*3, "", "", "Flem")
		Goto See
	Raise:
		POSS K 5
		POSS JIH 5
		Goto See
	Pain.head:
		POSS A 3 A_Pain
		//TNT1 A 0 A_log("headshot")
		goto see
	pain.leg:
		POSS A 3 A_Pain
		//TNT1 A 0 A_log("legshot")
		goto see
	Death.head:
		TNT1 A 0
		{
				A_Stop;
				A_StartSound("gore/headshot", CHAN_BODY);
				A_SpawnItem("PB_ZombiemanHeadExplode", 0, 40);
				A_XScream;
				A_SpawnItem("BloodSplasher2");
				A_NoBlocking;
		}
		//TNT1 A 0 A_Jump(32, "Deathchickenhead")
		POSH AABB 2
		{
			A_SpawnItemEx("NashGoreBloodSpurt",
			frandom(-5, 5), frandom(-5, 5), frandom(48, 52),
			frandom(-2, 2), frandom(-2, 2), frandom(4, 8),
			frandom(0, 360));
		}
		POSH CCDDEEEEFFFF 2 A_SpawnProjectile ("PB_SquirtingBloodTrail", 30, 0, random (0, 360), CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_BADPITCH|CMF_SAVEPITCH, random (60, 120))
		TNT1 A 0
		{
			A_SpawnItem("GrowingBloodPool");
			A_StartSound("BODYF",CHAN_AUTO);
		}
		POSH GGGGHHHHHHHHHHHHHHHH 2 A_SpawnProjectile ("PB_SquirtingBloodTrail", 10, 0, random (0, 360), CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_BADPITCH|CMF_SAVEPITCH, random (10, 40))
		TNT1 A 0 A_SpawnItem ("DeadFlemoid_NoHeadFront")
		Stop
    }
}